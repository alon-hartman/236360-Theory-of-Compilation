%{
    #include <iostream>
    #include "hw3_output.hpp"
    #include "source.hpp"
    #include "allocator.h"
    #include "bp.hpp"
    #include "utils.hpp"
    using namespace std;
    using namespace output;
    
    int yylex();
    void yyerror(char const*);
    
    extern int yylineno;

    SymTable &sym = SymTable::getInstance();

    #define YYERROR_VERBOSE 1
    #define YYDEBUG 1
%}

%token ID
%token VOID
%token COMMA
%token SC
%token RETURN
%token WHILE
%token BREAK
%token CONTINUE
%token B
%token NUM
%token TRUE
%token FALSE
%token INT
%token BYTE
%token BOOL
%left IF
%nonassoc ASSIGN
%left OR
%left AND
%left EQUALITY
%left RELATIONAL
%left ADDITIVE
%left MULTIPLICATIVE
%nonassoc NOT
%left LPAREN RPAREN LBRACE RBRACE
%nonassoc STRING
%nonassoc ELSE

%%
Program: Funcs
{ 
  if(yychar != YYEOF) {
    errorSyn(yylineno);
    exit(0);
 }
 $$ = $1;
 if(!sym.has_main) {
    errorMainMissing();
    exit(0);
 }
  sym.pop();
  CodeBuffer::instance().printGlobalBuffer();
  CodeBuffer::instance().printCodeBuffer();
}

Funcs: FuncDecl Funcs
{ 
  $$ = new Node("", yylineno);
  $$->setType($2);
  Delete(2, $1, $2); 
}
       | /*epsilon*/                            
{ 
  $$ = new Node("", yylineno); 
}
;
FuncDecl: RetType ID LPAREN {sym.push(SymTable::scope_type::FUNC);} Formals RPAREN
          { Node node($2->getName(), yylineno);
            node.m_type = $1->m_type;
            node.m_types_list = $5->m_types_list;
            sym.insert(&node, true);  // inserts function into scope
            sym.curr_func_ptr = openFunctionStack($1, $2, $5);
            emitGoto($1->next_list);  // attach goto address to somewhere
          }
          LBRACE Statements RBRACE     
{ 
  $$ = new Node($2->getName(), yylineno);
  $$->setType($1);
  $$->m_types_list = std::move($5->m_types_list);
  if($2->m_name == "main" && ($1->m_type != types::Void || $$->m_types_list.size() != 0)) {
    errorDef($2->m_lineno, $2->m_name);
    exit(0);
  }
  CodeBuffer::instance().bpatch($1->next_list, $9->m_label);
  emitFunctionReturn($$, $1);
  CodeBuffer::instance().bpatch($9->next_list, $$->m_label);  // goto in case of no goto in statement
  CodeBuffer::instance().emit("}");
  sym.pop();
  Delete(8, $1, $2, $3, $5, $6, $8, $9, $10); 
}
;
RetType: Type                                                       { $$ = $1; }
        | VOID                                                      { $$ = $1; }
;
Formals: FormalsList                                                { $$ = $1; }
        | /*epsilon*/                                               { $$ = new Node("", yylineno); }
; 
FormalsList: FormalDecl                                             { $$ = $1; }
        | FormalDecl COMMA FormalsList
{ 
  $$ = $1;
  $$->m_types_list.insert($$->m_types_list.end(),
                          $3->m_types_list.begin(),
                          $3->m_types_list.end());
  Delete(2, $2, $3); 
}
;
FormalDecl: Type ID
{ 
  $$ = new Node($2->getName(), yylineno);
  $$->setType($1);
  $$->m_types_list.push_back($1->m_type);
  Delete(2, $1, $2);
  sym.insert_arg($$);
}
; 
Statements: Statement
{ 
  $$ = $1;
  emitGoto($$->next_list);
}  // TODO: think about next line: we need that each statement will be a basic block. whats the best implementation?
            | Statements Statement
{ 
  $$ = $1;
  $$->m_types_list.push_back($2->m_type);
  CodeBuffer::instance().bpatch($1->next_list, $2->m_label);
  $$->false_list = CodeBuffer::instance().merge($1->false_list, $2->false_list);
  $$->true_list = CodeBuffer::instance().merge($1->true_list, $2->true_list);
  $$->next_list = $2->next_list;
  emitGoto($$->next_list);
  Delete(1, $2);
}
; 
Statement: LBRACE M1 Statements RBRACE
{ 
  $$ = $3;
  // Removed in CHECKUPS ???
  sym.pop(); 
  Delete(2, $1, $4);
}
            | Type ID SC
{ 
  $$ = $2;
  $$->setType($1);
  Delete(2, $1, $3);
  $$->m_label = CodeBuffer::instance().genLabel();
  sym.insert($$);
}
            | Type ID ASSIGN Exp SC
{
  if(!allowed_implicit_assignment($1->m_type, $4->m_type)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = $2;
  $$->setType($1);
  if ($4->m_type == types::Bool)
    emitBooleanAssignment($4);
  $$->m_reg = $4->m_reg;
  $$->m_label = $4->m_label;
  CodeBuffer::instance().bpatch($4->next_list, CodeBuffer::instance().genLabel());
  sym.insert($$, false, $4);
  Delete(4, $3, $4, $5, $1);
}
            | ID ASSIGN Exp SC
{
  SymTable::Entry *entry = sym.find_entry($1->m_name);
  if(entry == nullptr || entry->is_func) {
    errorUndef(yylineno, $1->m_name);
    exit(0);
  }
  if(!allowed_implicit_assignment(entry->return_type, $3->m_type)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = $1;
  $$->m_label = $3->m_label;
  $$->m_type = entry->return_type;
  if ($3->m_type == types::Bool)
    emitBooleanAssignment($3);
  CodeBuffer::instance().bpatch($3->next_list, CodeBuffer::instance().genLabel());
  storeValInStack(sym.curr_func_ptr, $3->m_reg, entry->offset, $3);
  Delete(3, $2, $3, $4);
}
            | Call SC
{
  $$ = $1;
  $$->setType($1);
  Delete(1, $2); 
}
            | RETURN SC
{
  SymTable::Entry &curr_func = sym.scopes_stack[0].entries.back();
  if(curr_func.return_type != types::Void) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node($1->getName(), yylineno);
  $$->m_type = types::Void;
  emitFunctionReturn($$);
  Delete(2, $1, $2);
}
            | RETURN Exp SC
{ 
  SymTable::Entry &curr_func = sym.scopes_stack[0].entries.back();
  if(curr_func.return_type == types::Void || 
     !allowed_implicit_assignment(curr_func.return_type, $2->m_type)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->setType($2);
  if ($2->m_type == types::Bool)
    emitBooleanAssignment($2);
  emitFunctionReturn($$, $2, curr_func.return_type);
  CodeBuffer::instance().bpatch($2->next_list, $$->m_label);
  $$->m_label = std::move($2->m_label);
  $$->m_reg = std::move($2->m_reg);
  Delete(3, $1, $2, $3);
}
            | IF MIF LPAREN Exp RPAREN MCE Statement
{
  $$ = new Node($1->getName(), yylineno);
  $$->setType($7);
  CodeBuffer &cb = CodeBuffer::instance();
  createBooleanLists($4);
  cb.bpatch($4->true_list, $7->m_label);
  $$->next_list = cb.merge($4->false_list, $7->next_list);
  $$->m_label = $4->m_label;
  $$->false_list = $7->false_list;  // break hack
  $$->true_list = $7->true_list;  // continue hack
  emitGoto($$->next_list);
  Delete(5, $1, $3, $4, $5, $7);
  sym.pop();
}
            | IF MIF LPAREN Exp RPAREN MCE Statement ELSE { emitGoto($7->next_list); }
              MELSE Statement
{ 
  $$ = new Node("", yylineno);
  $$->setType($7);
  CodeBuffer &cb = CodeBuffer::instance();
  createBooleanLists($4);
  $$->next_list = cb.merge($7->next_list, $11->next_list);
  $$->m_label = $4->m_label;
  $$->false_list = cb.merge($7->false_list, $11->false_list);  // break hack
  $$->true_list = cb.merge($7->true_list, $11->true_list);  // continue hack
  cb.bpatch($4->true_list, $7->m_label);
  cb.bpatch($4->false_list, $11->m_label);
  emitGoto($$->next_list);
  Delete(7, $1, $3, $4, $5, $7, $8, $11); 
  sym.pop();
}
            | WHILE MWHILE LPAREN Exp RPAREN MCE Statement
{ 
  $$ = new Node($1->getName(), yylineno);
  $$->setType($7);  // statement may contain return value
  CodeBuffer &cb = CodeBuffer::instance();
  emitGoto($7->next_list);
  createBooleanLists($4);
  $$->m_label = $4->m_label;
  cb.bpatch($7->next_list, $4->m_label);
  cb.bpatch($4->true_list, $7->m_label);
  $$->next_list = cb.merge($4->false_list, $7->false_list);  // break hack
  cb.bpatch($7->true_list, $4->m_label);  // continue hack
  Delete(5, $1, $3, $4, $5, $7);
  sym.pop();
}
            | BREAK SC
{ 
  if(!sym.isInScope(SymTable::scope_type::WHILE)) {
    errorUnexpectedBreak(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->m_label = CodeBuffer::instance().genLabel();
  emitGoto($$->false_list);
  Delete(2, $1, $2);
}
            | CONTINUE SC
{ 
  if(!sym.isInScope(SymTable::scope_type::WHILE)) {
    errorUnexpectedContinue(yylineno);
    exit(0);
  }
  $$ = new Node($1->getName(), yylineno);
  $$->setType($1);
  $$->m_label = CodeBuffer::instance().genLabel();
  emitGoto($$->true_list);
  Delete(2, $1, $2);
}
; 
Call: ID LPAREN ExpList RPAREN
{
  SymTable::Entry *entry = sym.find_entry($1->m_name);
  if(entry == nullptr || (entry->is_func == false)) {
    errorUndefFunc(yylineno, $1->m_name);
    exit(0);
  }
  check_args_type($3, entry);
  $$ = new Node($1->getName(), yylineno);
  $$->m_type = entry->return_type;
  $$->m_types_list = std::move($3->m_types_list);
  $$->m_reg_list = std::move($3->m_reg_list);
  emitFunctionCall($$, entry);
  CodeBuffer::instance().bpatch($3->next_list, $$->m_label);
  $$->m_label = $3->m_label;
  Delete(4, $3, $4, $1, $2);
}
    | ID LPAREN RPAREN
{ 
  SymTable::Entry *entry = sym.find_entry($1->m_name);
  if(entry == nullptr || (entry->is_func == false)) {
    errorUndefFunc(yylineno, $1->m_name);
    exit(0);
  }
  if(entry->types_vec.size() != 0) {
    std::vector<std::string> strings_vec = TypesToStrings(entry->types_vec);
    errorPrototypeMismatch(yylineno, entry->name, strings_vec);
    exit(0);
  }
  $$ = new Node($1->getName(), yylineno);
  $$->m_type = entry->return_type;
  emitFunctionCall($$, entry);
  Delete(3, $1, $2, $3);
} %prec OR
; 
ExpList: Exp
{ 
  $$ = $1;
  $$->m_types_list.push_back($$->m_type);
    CodeBuffer &cb = CodeBuffer::instance();
    if ($1->m_type == types::Bool)
      emitBooleanAssignment($$);
  if($$->m_reg == "") {
    string label = cb.genLabel();
    $$->m_reg = $$->getReg();
    emitGoto($$->next_list);
    cb.bpatch($$->next_list, label);
    if($$->m_label == "")
      $$->m_label = label;
  }
  $$->m_reg_list.push_back($$->m_reg);
}
    | Exp COMMA ExpList
{ 
  $$ = $1;
  $$->m_types_list.push_back($$->m_type);
  $$->m_types_list.insert($$->m_types_list.end(),
                          $3->m_types_list.begin(),
                          $3->m_types_list.end());
    CodeBuffer &cb = CodeBuffer::instance();
    if ($1->m_type == types::Bool)
      emitBooleanAssignment($$);
  if($$->m_reg == "") {
    string label = cb.genLabel();
    $$->m_reg = $$->getReg();
    emitGoto($$->next_list);
    cb.bpatch($$->next_list, label);
    if($$->m_label == "")
      $$->m_label = label;
  }
  $$->m_reg_list.push_back($$->m_reg);
  $$->m_reg_list.insert($$->m_reg_list.end(),
                          $3->m_reg_list.begin(),
                          $3->m_reg_list.end());
  CodeBuffer::instance().bpatch($1->next_list, $3->m_label);
  $$->next_list = $3->next_list;
  Delete(2, $2, $3);
}
; 
Type: INT                                 { $$ = $1; }
    | BYTE                                { $$ = $1; }
    | BOOL                                { $$ = $1; }
; 
Exp : LPAREN Exp RPAREN                   { $$ = $2; }
    | Exp IF LPAREN Exp RPAREN ELSE Exp
{ 
  if($4->m_type != types::Bool ||
     !(allowed_implicit_assignment($1->m_type, $7->m_type) || 
       allowed_implicit_assignment($7->m_type, $1->m_type))) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->setType($1);
  if($1->m_type == types::Int || $7->m_type == types::Int) {
    $$->m_type = types::Int;
  }
  $$->m_label = $4->m_label;
  createBooleanLists($4);
  if ($1->m_type == types::Bool) {
    emitBooleanAssignment($1);
    emitBooleanAssignment($7);
  }
  CodeBuffer &cb = CodeBuffer::instance();
  cb.bpatch($4->true_list, $1->m_label);
  cb.bpatch($4->false_list, $7->m_label);
  $$->m_reg = Allocator::getInstance().fresh_var("phiReg");
  std::string phi_label = cb.genLabel();
  CodeBuffer::instance().emit("\t" + $$->m_reg + " = phi i32 [" + $1->m_reg + ", %" + $1->m_label + "]," + " [" + $7->m_reg + ", %" + $7->m_label + "]");
  cb.bpatch($1->next_list, phi_label);
  cb.bpatch($7->next_list, phi_label);
  emitGoto($$->next_list);
  
  Delete(7, $1, $2, $3, $4, $5, $6, $7);
} %prec IF
    | Exp ADDITIVE Exp
{ 
  if(!$1->isNumType() || !$3->isNumType()) {
      errorMismatch(yylineno);
      exit(0);
    }
    $$ = new Node("", yylineno);
    $$->setType($1);
    if($1->m_type == types::Int || $3->m_type == types::Int) {
      $$->m_type = types::Int;  // result has bigger type
    }
    emitBinopRes($1, $3, $2->m_name, $$);
    Delete(3, $1, $2, $3);
}
    | Exp MULTIPLICATIVE Exp
{
  if(!$1->isNumType() || !$3->isNumType()) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->setType($1);
  if($1->m_type == types::Int || $3->m_type == types::Int) {
    $$->m_type = types::Int;  // result has bigger type
  }
  emitBinopRes($1, $3, $2->m_name, $$);
  Delete(3, $1, $2, $3);
}
    | ID
{ 
  $$ = $1;
  SymTable::Entry *entry = sym.find_entry($1->m_name);
  
    if(!entry || entry->is_func) {
    errorUndef(yylineno, $1->m_name);
    exit(0);
  }
  $$->m_type = entry->return_type;
  if($$->isNumType() || $$->m_type == types::Bool)
    emitExpGoto($$);
}
    | Call
{ 
  $$ = new Node("", yylineno);
  $$->setType($1);
  $$->next_list = std::move($1->next_list);
  $$->m_label = std::move($1->m_label);
  $$->m_reg = std::move($1->m_reg);
  Delete(1, $1);
}
    | NUM
{ 
  $$ = $1;
  emitExpGoto($$);
}
    | NUM B
{ 
  $$ = new ByteType(($1->m_name).c_str(), yylineno);
  Delete(2, $1, $2);
  emitExpGoto($$);
}
    | STRING
{ 
  $$ = $1;
  $$->m_reg = emitStringDeclarationAndAssignment($$);
}
    | TRUE
{ 
  $$ = new BoolType("true", yylineno);
  emitExpGoto($$);
  $$->true_list = $$->next_list;
  Delete(1, $1);
}
    | FALSE
{ 
  $$ = new BoolType("false", yylineno);
  emitExpGoto($$);
  $$->false_list = $$->next_list;
  Delete(1, $1); 
}
    | NOT Exp
{ 
  if(($2->m_type != types::Bool)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->m_type = types::Bool;
  $$->m_label = $2->m_label;
  $$->m_reg = $2->m_reg;
  createBooleanLists($2);
  $$->true_list = $2->false_list;
  $$->false_list = $2->true_list;
  Delete(2, $1, $2); 
}
    | Exp AND Exp
{ 
  if(($1->m_type != types::Bool) || ($3->m_type != types::Bool)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->m_type = types::Bool;
  $$->m_label = $1->m_label;
  CodeBuffer &cb = CodeBuffer::instance();
  createBooleanLists($1);
  createBooleanLists($3);
  emitBooleanBlockShortCircuit($1, $3, "AND", $$);
  Delete(3, $1, $2, $3);
}
    | Exp OR Exp
{ 
  if(($1->m_type != types::Bool) || ($3->m_type != types::Bool)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->m_label = $1->m_label;
  $$->m_type = types::Bool;
  CodeBuffer &cb = CodeBuffer::instance();
  createBooleanLists($1);
  createBooleanLists($3);
  emitBooleanBlockShortCircuit($1, $3, "OR", $$);
  Delete(3, $1, $2, $3);
}
    | Exp EQUALITY Exp
{ 
  if(!($1->isNumType()) || !($3->isNumType())) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->m_type = types::Bool;
  emitBooleanBlockRelaEq($1, $3, $2->m_name, $$);
  Delete(3, $1, $2, $3);
}
    | Exp RELATIONAL Exp
{ 
  if(!($1->isNumType()) || !($3->isNumType())) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node("", yylineno);
  $$->m_type = types::Bool;
  emitBooleanBlockRelaEq($1, $3, $2->m_name, $$);
  Delete(3, $1, $2, $3);
}
    | LPAREN Type RPAREN Exp
{
  if(!allowed_explicit_assignment($2->m_type, $4)) {
    errorMismatch(yylineno);
    exit(0);
  }
  $$ = new Node($4->m_name.c_str(), yylineno);
  $$->m_type = $2->m_type;
  $$->m_lineno = $2->m_lineno;
  $$->m_reg = Allocator::getInstance().fresh_var("explicitConvReg");
  $$->m_label = $4->m_label;
  CodeBuffer::instance().bpatch($4->next_list, CodeBuffer::instance().genLabel());
  emitTypeConversion($$, $4);
  emitGoto($$->next_list);
  Delete(4, $1, $2, $3, $4);
}
;
// open scope:
M1 : { sym.push(); }
MIF : { sym.push(SymTable::scope_type::IF); }
MELSE : { sym.pop(); sym.push(SymTable::scope_type::ELSE); }
MWHILE : { sym.push(SymTable::scope_type::WHILE); }
MCE: { if(yyvsp[-1]->m_type != types::Bool) {
           errorMismatch(yyvsp[-1]->m_lineno);
           exit(0);
       }
       if(yyvsp[-1]->m_label == "" && yyvsp[-1]->m_name != "")  // only "true" or "false", not "true and false"
           emitBooleanNoOp(yyvsp[-1]); } // Check Exp
;
%%

int main()
{
    /* yydebug = 1; */
    return yyparse();
}

void yyerror(char const* s) {
    errorSyn(yylineno);
    exit(0);
}